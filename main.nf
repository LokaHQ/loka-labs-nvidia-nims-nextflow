#!/usr/bin/env nextflow

nextflow.enable.dsl = 2

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    nf-binder-design
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Protein binder design pipeline with multiple methods
----------------------------------------------------------------------------------------
*/

// Method parameter for workflow selection
params.method = false
params.outdir = 'results'

// Conditional includes based on --method parameter
if (params.method == "rfd") {
    include { RFD } from './workflows/rfd'
} else if (params.method == "rfd_nim") {
    include { RFD_NIM } from './workflows/rfd_nim'
} else if (params.method == "rfd_openfold") {
    include { RFD_OPENFOLD } from './workflows/rfd_openfold'
} else if (params.method == "rfd_partial") {
    include { RFD_PARTIAL } from './workflows/rfd_partial'
} else if (params.method == "bindcraft") {
    include { BINDCRAFT } from './workflows/bindcraft'
} else if (params.method == "germinal") {
    include { GERMINAL } from './workflows/germinal'
} else if (params.method == "boltzgen") {
    include { BOLTZGEN } from './workflows/boltzgen'
} else if (params.method == "boltz_pulldown") {
    include { BOLTZ_PULLDOWN } from './workflows/boltz_pulldown'
} else if (params.method == "rfd3") {
    include { RFD3 } from './workflows/rfd3'
} else if (params.method == "foldseek") {
    include { FOLDSEEK } from './workflows/foldseek'
}

def paramsToMap(params) {
    def map = [:]
    params.each { key, value ->
        if (value instanceof Path || value instanceof File) {
            map[key] = value.toString()
        }
        else if (!(value instanceof Closure) && !(key in [
            'class',
            'launchDir',
            'projectDir',
            'workDir',
        ])) {
            map[key] = value
        }
    }
    return map
}

workflow {

    main:

    // Show help if no method specified
    if (params.method == false) {
        log.info(
            """
        ==================================================================
        PROTEIN BINDER DESIGN PIPELINE
        ==================================================================

        Usage: nextflow run main.nf --method <method> [options]

        Available methods:
            rfd             RFDiffusion-based binder design
            rfd_nim         RFDiffusion+ProteinMPNN NIM proof-of-concept (see workflows/rfd_nim.nf)
            rfd_openfold    RFDiffusion-based binder design, OpenFold3 scoring
            rfd_partial     RFDiffusion partial diffusion for binder optimization
            rfd3            RFDiffusion3-based binder design
            bindcraft       BindCraft binder design
            germinal        Germinal antibody/nanobody design
            boltzgen        BoltzGen binder design
            boltz_pulldown  Boltz pulldown predictions
            foldseek        FoldSeek structural similarity search

        Example:
            nextflow run main.nf --method rfd --input_pdb target.pdb --rfd_n_designs 10

        For method-specific help, run with --method <method> and no other arguments.

        """.stripIndent()
        )
        exit(1)
    }

    // Dispatch to appropriate workflow
    if (params.method == "rfd") {
        RFD()
    } else if (params.method == "rfd_nim") {
        RFD_NIM()
    } else if (params.method == "rfd_openfold") {
        RFD_OPENFOLD()
    } else if (params.method == "rfd_partial") {
        RFD_PARTIAL()
    } else if (params.method == "bindcraft") {
        BINDCRAFT()
    } else if (params.method == "germinal") {
        GERMINAL()
    } else if (params.method == "boltzgen") {
        BOLTZGEN()
    } else if (params.method == "boltz_pulldown") {
        BOLTZ_PULLDOWN()
    } else if (params.method == "rfd3") {
        RFD3()
    } else if (params.method == "foldseek") {
        FOLDSEEK()
    } else {
        log.error("Unknown method: ${params.method}")
        log.info("Available methods: rfd, rfd_nim, rfd_openfold, rfd_partial, rfd3, bindcraft, germinal, boltzgen, boltz_pulldown, foldseek")
        exit(1)
    }

    workflow.onComplete = {
        // Write the pipeline parameters to a JSON file
        def params_json = [:]

        params_json['params'] = paramsToMap(params)

        params_json['workflow'] = [
            name: workflow.manifest.name,
            version: workflow.manifest.version,
            revision: workflow.revision ?: null,
            commit: workflow.commitId ?: null,
            runName: workflow.runName,
            start: workflow.start.format('yyyy-MM-dd HH:mm:ss'),
            complete: workflow.complete.format('yyyy-MM-dd HH:mm:ss'),
            duration: workflow.duration,
            success: workflow.success,
        ]

        def output_file = "${params.outdir}/params.json"
        def json_string = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(params_json))
        def output_path = file(output_file)

        if (output_path.fileSystem.provider().scheme == 'file') {
            java.nio.file.Files.createDirectories(output_path.parent)
        }
        output_path.text = json_string

        log.info("Pipeline parameters saved to: ${output_file}")
    }
}
